#!/bin/zsh

# Tool to copy and paste dotfiles.
# Adam Heins

here=${0:a:h}

DOTFILES=(.zsh .zshrc
          .gitconfig .gitignore
          .vimrc .vim
          .tmux.conf
          .editrc
          .inputrc
          .dircolors
          .vimperatorrc)

# Check that dependencies are installed.
command -v rsync >/dev/null 2>&1 || { echo >&2 "rsync not installed, aborting"; exit 1; }

# Check that the `files' directory exists.
[ -d files ] || { echo >&2 "no dotfiles are here"; exit 1; }

# Create a directory to hold a backup of the current dotfiles on this machine.
backup_dir="backup_$(date +%Y-%m-%d_%H-%M-%S)"
mkdir $here/$backup_dir

# Create a symlink to the current backup directory.
readlink $here/backup > /dev/null && rm $here/backup
ln -s $here/$backup_dir $here/backup

for file in $DOTFILES; do
  # Backup each local file.
  [ -e ~/"$file" ] && rsync -r ~/"$file" $here/backup

  # Remove existing local copy (if needed) and create a symlink to the one in
  # this repo.
  if [ -e "files/$file" ]; then
    [ -e ~/"$file" ] && rm -rf ~/"$file"
    ln -s $here/files/$file ~/"$file"
  fi
done

# Create vim aliases symlink.
ln -s ~/.vim/aliases.vim ~/.vim/after/plugin/aliases.vim
